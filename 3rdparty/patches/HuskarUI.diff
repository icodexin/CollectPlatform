diff --git a/CMakeLists.txt b/CMakeLists.txt
index 9a402aa..79907e0 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -36,7 +36,11 @@ if(BUILD_HUSKARUI_ON_DESKTOP_PLATFORM)
     add_subdirectory(3rdparty/qwindowkit)
 endif()
 
-set(CMAKE_INSTALL_PREFIX ${CMAKE_CURRENT_SOURCE_DIR})
+if (CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
+    set(CMAKE_INSTALL_PREFIX ${CMAKE_CURRENT_SOURCE_DIR}/output)
+    message(STATUS "Using default install prefix: ${CMAKE_INSTALL_PREFIX}")
+endif ()
+
 add_subdirectory(src)
 
 if(BUILD_HUSKARUI_GALLERY)
diff --git a/src/CMakeLists.txt b/src/CMakeLists.txt
index 1cad2e2..1ebcf9d 100644
--- a/src/CMakeLists.txt
+++ b/src/CMakeLists.txt
@@ -83,6 +83,8 @@ if(WIN32)
     )
 endif()
 
+qt_policy(SET QTP0004 NEW)
+
 qt_add_qml_module(${PROJECT_NAME}
     URI "HuskarUI.Basic"
     VERSION 1.0
@@ -262,12 +264,18 @@ target_link_libraries(${PROJECT_NAME} PRIVATE
     $<$<BOOL:${BUILD_HUSKARUI_ON_DESKTOP_PLATFORM}>:QWKQuick>
 )
 
+include(GNUInstallDirs)
+set(INCLUDE_INSTALL_DIR "${CMAKE_INSTALL_INCLUDEDIR}/HuskarUI")
+set(QML_INSTALL_DIR "qml/HuskarUI")
+set(CMAKE_CONFIG_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}/cmake/HuskarUI")
+
 target_include_directories(${PROJECT_NAME} PUBLIC
-    ${CMAKE_CURRENT_SOURCE_DIR}
-    ${CMAKE_CURRENT_SOURCE_DIR}/cpp
-    ${CMAKE_CURRENT_SOURCE_DIR}/cpp/theme
-    ${CMAKE_CURRENT_SOURCE_DIR}/cpp/controls
-    ${CMAKE_CURRENT_SOURCE_DIR}/cpp/utils
+    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
+    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/cpp>
+    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/cpp/theme>
+    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/cpp/controls>
+    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/cpp/utils>
+    $<INSTALL_INTERFACE:${INCLUDE_INSTALL_DIR}>
 )
 
 #Gen HuskarUI/include
@@ -278,24 +286,50 @@ if(HUSKARUI_HEADER_OUTPUT_DIRECTORY)
          PATTERN "*_p.h" EXCLUDE)
 endif()
 
-install(TARGETS ${PROJECT_NAME}
-    RUNTIME DESTINATION ${CMAKE_INSTALL_PREFIX}/HuskarUI/bin
-    ARCHIVE DESTINATION ${CMAKE_INSTALL_PREFIX}/HuskarUI/lib
-    LIBRARY DESTINATION ${CMAKE_INSTALL_PREFIX}/HuskarUI/lib
+install(TARGETS ${PROJECT_NAME} ${PLUGIN_NAME}
+    EXPORT HuskarUITargets
+    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
+    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
+    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
 )
 
 install(
     FILES cpp/husglobal.h cpp/husapp.h cpp/husdefinitions.h
-    DESTINATION ${CMAKE_INSTALL_PREFIX}/HuskarUI/include
+    DESTINATION ${INCLUDE_INSTALL_DIR}
 )
 
 install(
     DIRECTORY cpp/controls cpp/theme cpp/utils
-    DESTINATION ${CMAKE_INSTALL_PREFIX}/HuskarUI/include
+    DESTINATION ${INCLUDE_INSTALL_DIR}
     FILES_MATCHING PATTERN "*.h"
     PATTERN "*_p.h" EXCLUDE
 )
 
 install(
-    DIRECTORY ${HUSKARUI_PLUGIN_OUTPUT_DIRECTORY} DESTINATION ${CMAKE_INSTALL_PREFIX}/HuskarUI/imports/HuskarUI
+    DIRECTORY ${HUSKARUI_PLUGIN_OUTPUT_DIRECTORY} DESTINATION ${QML_INSTALL_DIR}
+)
+
+install(EXPORT HuskarUITargets
+    FILE HuskarUITargets.cmake
+    DESTINATION ${CMAKE_CONFIG_INSTALL_DIR}
+)
+
+include(CMakePackageConfigHelpers)
+
+configure_package_config_file(
+    ${CMAKE_CURRENT_SOURCE_DIR}/HuskarUIConfig.cmake.in
+    ${CMAKE_CURRENT_BINARY_DIR}/HuskarUIConfig.cmake
+    INSTALL_DESTINATION ${CMAKE_CONFIG_INSTALL_DIR}
+)
+
+write_basic_package_version_file(
+    ${CMAKE_CURRENT_BINARY_DIR}/HuskarUIConfigVersion.cmake
+    VERSION ${HUSKARUI_VERSION}
+    COMPATIBILITY SameMajorVersion
+)
+
+install(FILES
+    ${CMAKE_CURRENT_BINARY_DIR}/HuskarUIConfig.cmake
+    ${CMAKE_CURRENT_BINARY_DIR}/HuskarUIConfigVersion.cmake
+    DESTINATION ${CMAKE_CONFIG_INSTALL_DIR}
 )
diff --git a/src/HuskarUIConfig.cmake.in b/src/HuskarUIConfig.cmake.in
new file mode 100644
index 0000000..5b1691a
--- /dev/null
+++ b/src/HuskarUIConfig.cmake.in
@@ -0,0 +1,3 @@
+@PACKAGE_INIT@
+
+include("${CMAKE_CURRENT_LIST_DIR}/HuskarUITargets.cmake")
\ No newline at end of file
diff --git a/src/imports/HusMenu.qml b/src/imports/HusMenu.qml
index 7ef3350..1daac48 100644
--- a/src/imports/HusMenu.qml
+++ b/src/imports/HusMenu.qml
@@ -235,6 +235,11 @@ Item {
         __listView.model = [];
     }
 
+    function clearSelectedMenu() {
+        __private.gotoMenuKey = '';
+        __private.selectedItem = null;
+    }
+
     component MenuButton: HusButton {
         id: __menuButtonImpl
 
